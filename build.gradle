plugins {
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'java'
	// ADDED for docker plugin
	id 'com.palantir.docker' version '0.34.0'
}

// ADDED as source of plugins
repositories {
  mavenCentral()
}

group = 'org.fabianlee'
version = '1.0.0'
sourceCompatibility = '17'

// ADDED want consistent name for jar
bootJar {
  archiveFileName = "springBoot.jar"
}

// ADDED ability to specify docker version on command line
ext.dockerVersion = project.hasProperty('dockerVersion') ? project.getProperty('dockerVersion'):'1.0.0'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}


// ADDED build OCI image using Docker
docker {
    group "OCI"
    dependsOn bootJar
    name "spring-boot-github-action:${dockerVersion}"
    files 'build/libs/springBoot.jar'
    dockerfile file('src/main/resources/Dockerfile')
}


// ADDED build OCI image using Podman
task copyDockerfile(type: Copy) {
  from 'src/main/resources/Dockerfile'
  into 'build/libs'
}
task buildah(type: Exec) {
    group "OCI"
    dependsOn bootJar
    dependsOn copyDockerfile
    workingDir "${buildDir}/libs"
    commandLine "buildah", "bud", "-f", "Dockerfile", "-t", "${project.name}:${dockerVersion}"
}

